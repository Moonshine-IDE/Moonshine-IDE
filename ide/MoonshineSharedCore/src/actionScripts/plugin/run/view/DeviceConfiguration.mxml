<?xml version="1.0" encoding="utf-8"?>
<resizableTitleWindow:ResizableTitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
										   xmlns:s="library://ns.adobe.com/flex/spark" 
										   xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:resizableTitleWindow="actionScripts.ui.resizableTitleWindow.*" 
										   width="600" height="240" minWidth="300" minHeight="150"
										   skinClass="components.skins.ResizableTitleWindowSkin"
										   title="Device Configurations" xmlns:view="actionScripts.plugin.run.view.*"
										   initialize="onInitialized(event)">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.core.FlexGlobals;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import actionScripts.events.GeneralEvent;
			import actionScripts.events.ProjectEvent;
			import actionScripts.valueObjects.ConstantsCoreVO;
			import actionScripts.valueObjects.MobileDeviceVO;
			
			import components.popup.DeviceDefinePopup;
			
			[Bindable] public var devices:ArrayCollection;
			
			private var deviceDefinePop:DeviceDefinePopup;
			
			protected function onDeviceAddition(event:MouseEvent, isNew:Boolean):void
			{
				if (!deviceDefinePop)
				{
					deviceDefinePop = PopUpManager.createPopUp(FlexGlobals.topLevelApplication as DisplayObject, DeviceDefinePopup, true) as DeviceDefinePopup;
					deviceDefinePop.addEventListener(CloseEvent.CLOSE, onDefinePopupClosed);
					deviceDefinePop.addEventListener(GeneralEvent.DONE, onDeviceUpdated);
					if (!isNew) deviceDefinePop.editedDevice = dgDevices.selectedItem as MobileDeviceVO;
					PopUpManager.centerPopUp(deviceDefinePop);
				}
				else
				{
					PopUpManager.bringToFront(deviceDefinePop);
				}
			}
			
			protected function onDeviceDeletion(event:MouseEvent):void
			{
				if (dgDevices.selectedItem.type == MobileDeviceVO.AND) ConstantsCoreVO.TEMPLATES_ANDROID_DEVICES.removeItem(dgDevices.selectedItem);
				else ConstantsCoreVO.TEMPLATES_IOS_DEVICES.removeItem(dgDevices.selectedItem);
				devices.removeItem(dgDevices.selectedItem);
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			private function onDefinePopupClosed(event:CloseEvent):void
			{
				deviceDefinePop.removeEventListener(CloseEvent.CLOSE, onDefinePopupClosed);
				deviceDefinePop.removeEventListener(ProjectEvent.FLEX_SDK_UDPATED, onDeviceUpdated);
				deviceDefinePop = null;
				
				System.gc();
			}
			
			private function onDeviceUpdated(event:GeneralEvent):void
			{
				onDefinePopupClosed(null);
				
				if (devices.getItemIndex(event.value) == -1)
				{
					devices.addItem(event.value);
					if (event.value.type == MobileDeviceVO.AND) ConstantsCoreVO.TEMPLATES_ANDROID_DEVICES.addItem(event.value);
					else ConstantsCoreVO.TEMPLATES_IOS_DEVICES.addItem(event.value);
				}
				else
					devices.refresh();
				
				dispatchEvent(new Event(Event.CHANGE));
			}
			
			protected function onInitialized(event:FlexEvent):void
			{
				devices = new ArrayCollection();
				devices.addAll(ConstantsCoreVO.TEMPLATES_ANDROID_DEVICES);
				devices.addAll(ConstantsCoreVO.TEMPLATES_IOS_DEVICES);
			}
			
		]]>
	</fx:Script>
	
	<s:VGroup width="100%" height="100%">
		<s:DataGrid id="dgDevices" 
					dataProvider="{devices}"
					width="100%" height="100%"
					sortableColumns="false" borderVisible="false"
					contentBackgroundColor="0xe0e0e0" selectionColor="0xf6f6f6"
					rowHeight="28" horizontalScrollPolicy="off">
			<s:columns>
				<s:ArrayList>
					<s:GridColumn headerText="Name" dataField="name" width="200" minWidth="200"/>
					<s:GridColumn headerText="Key" dataField="key" width="200"/>
					<s:GridColumn headerText="Scale DPI" dataField="dpi" width="100"/>
					<s:GridColumn headerText="" dataField="type" minWidth="100"/>
				</s:ArrayList>
			</s:columns>
		</s:DataGrid>
		<s:HGroup width="100%"
				  paddingBottom="6" paddingRight="6" paddingLeft="6" paddingTop="4">
			<s:Button label="+"
					  click="onDeviceAddition(event, true)"
					  width="30"/>
			<s:Button label="-"
					  enabled="{dgDevices.selectedItem &amp;&amp; !dgDevices.selectedItem.isDefault}" width="30"
					  click="onDeviceDeletion(event)"/>
			<s:Button label="Edit"
					  enabled="{dgDevices.selectedItem}" 
					  click="onDeviceAddition(event, false)"/>
		</s:HGroup>
	</s:VGroup>
</resizableTitleWindow:ResizableTitleWindow>