<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
// 
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
// 
// http://www.apache.org/licenses/LICENSE-2.0 
// 
// Unless required by applicable law or agreed to in writing, software 
// distributed under the License is distributed on an "AS IS" BASIS, 
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and 
// limitations under the License
// 
// No warranty of merchantability or fitness of any kind. 
// Use this software at your own risk.
// 
////////////////////////////////////////////////////////////////////////////////
-->
<s:VGroup
		xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		width="100%"
		paddingLeft="15"
		paddingTop="15"
		paddingRight="15"
		paddingBottom="15"
		creationComplete="onPathRendererCreationComplete(event)"
		removedFromStage="onPathRendererRemovedFromStage(event)">
	<fx:Script>
		<![CDATA[
		import components.popup.OpenHiddenFilePathPopup;

		import mx.collections.ArrayCollection;
		import mx.controls.Alert;
		import mx.core.FlexGlobals;
		import mx.events.CloseEvent;
		import mx.events.FlexEvent;
		import mx.managers.PopUpManager;

		import spark.events.IndexChangeEvent;

		import actionScripts.events.GlobalEventDispatcher;
		import actionScripts.events.ProjectEvent;
		import actionScripts.factory.FileLocation;
		import actionScripts.locator.IDEModel;
		import actionScripts.plugin.settings.vo.AbstractSetting;
		import actionScripts.plugin.settings.vo.PathSetting;
		import actionScripts.utils.SDKUtils;
		import actionScripts.utils.UtilsCore;
		import actionScripts.valueObjects.SDKReferenceVO;

		import moonshine.components.SDKDefineView;
		import moonshine.components.SDKSelectorView;

		import actionScripts.ui.FeathersUIWrapper;
		import actionScripts.valueObjects.ConstantsCoreVO;

		import feathers.data.ArrayCollection;

		[Bindable]
		public var setting:PathSetting;
		[Bindable]
		public var isSDKPath:Boolean;
		[Bindable]
		public var isDropDown:Boolean;
		[Bindable]
		public var dropdownListItems:mx.collections.ArrayCollection;

		[Bindable]
		private var model:IDEModel = IDEModel.getInstance();

		private var sdkPopup:SDKSelectorView;
		private var sdkPopupWrapper:FeathersUIWrapper;
		private var sdkPathPopup:SDKDefineView;
		private var sdkPathPopupWrapper:FeathersUIWrapper;
		private var gotoFolderPopup:OpenHiddenFilePathPopup;

		private var message:String;
		private var messageType:String;

		public function setMessage(value:String, type:String):void
		{
			this.message = value;
			this.messageType = type;

			if (!lblMessage)
			{
				return;
			}
			if (!this.message)
			{
				lblMessage.text = "";
				lblMessage.includeInLayout = lblMessage.visible = false;
			}

			lblMessage.includeInLayout = lblMessage.visible = true;
			if (isDropDown && dropdownListItems && dropdownListItems.length == 0)
			{
				value = "Select path to continue";
				type = AbstractSetting.MESSAGE_CRITICAL;
			}

			switch (type)
			{
				case AbstractSetting.MESSAGE_CRITICAL:
					lblMessage.setStyle("color", 0xff0000);
					break;
				case AbstractSetting.MESSAGE_IMPORTANT:
					lblMessage.setStyle("color", 0x0099ff);
					break;
				default:
					lblMessage.setStyle("color", 0x666666);
					break;
			}

			lblMessage.text = value;
		}

		private function selectPath():void
		{
			// opens only if sdkpop not opens already
			if (isSDKPath)
			{
				if (!sdkPopup)
				{
					sdkPopup = new SDKSelectorView();
					sdkPopup.sdks = new feathers.data.ArrayCollection(model.userSavedSDKs.source);
					sdkPopupWrapper = new FeathersUIWrapper(sdkPopup);
					PopUpManager.addPopUp(sdkPopupWrapper, DisplayObject(parentApplication), true);
					sdkPopup.addEventListener(SDKSelectorView.EVENT_SDK_ADD, onSDKPopupAdd);
					sdkPopup.addEventListener(SDKSelectorView.EVENT_SDK_REMOVE, onSDKPopupRemove);
					sdkPopup.addEventListener(SDKSelectorView.EVENT_SDK_EDIT, onSDKPopupEdit);
					sdkPopup.addEventListener(Event.CLOSE, onSDKPopupClosed);
					PopUpManager.centerPopUp(sdkPopupWrapper);
					sdkPopupWrapper.assignFocus("top");
					sdkPopupWrapper.stage.addEventListener(Event.RESIZE, sdkPopup_stage_resizeHandler, false, 0, true);
				}
			}
			else
			{
				var location:FileLocation = new FileLocation(setting.stringValue);
				var startFromLocation:String = setting.defaultPath;
				if (location.fileBridge.isPathExists(setting.stringValue))
				{
					startFromLocation = location.fileBridge.nativePath;
				}

				if (setting.directory)
				{
					model.fileCore.browseForDirectory("Select directory", openFile, null, startFromLocation);
				}
				else
				{
					model.fileCore.browseForOpen("Select file", openFile, null, setting.fileFilters, startFromLocation);
				} // for Balsmiq project we need only .xml file to get selected.
			}
		}

		private function openPopupToAccessArbitraryFolder():void
		{
			if (!gotoFolderPopup)
			{
				gotoFolderPopup = PopUpManager.createPopUp(FlexGlobals.topLevelApplication as DisplayObject, OpenHiddenFilePathPopup, true) as OpenHiddenFilePathPopup;
				gotoFolderPopup.isDirectory = setting.directory;
				gotoFolderPopup.addEventListener(OpenHiddenFilePathPopup.EVENT_PATH_SELECTED, onGotoFolderPathSelected);
				gotoFolderPopup.addEventListener(CloseEvent.CLOSE, onGotoFolderWindowClosed);
				PopUpManager.centerPopUp(gotoFolderPopup);
			}
		}

		private function onGotoFolderWindowClosed(event:CloseEvent):void
		{
			gotoFolderPopup.removeEventListener(OpenHiddenFilePathPopup.EVENT_PATH_SELECTED, onGotoFolderPathSelected);
			gotoFolderPopup.removeEventListener(CloseEvent.CLOSE, onGotoFolderWindowClosed);
			gotoFolderPopup = null;
		}

		private function onGotoFolderPathSelected(event:Event):void
		{
			openFile(gotoFolderPopup.path);
		}

		private function sdkPopup_stage_resizeHandler(event:Event):void
		{
			PopUpManager.centerPopUp(sdkPopupWrapper);
		}

		private function clearPath():void
		{
			setting.stringValue = "";
			lblMessage.text = "";
		}

		private function onSDKPopupAdd(event:Event):void
		{
			this.editSDK(null);
		}

		private function onSDKPopupRemove(event:Event):void
		{
			var sdk:SDKReferenceVO = sdkPopup.selectedSDK;
			model.userSavedSDKs.removeItem(sdk);
			sdkPopup.sdks = new feathers.data.ArrayCollection(model.userSavedSDKs.source);
			GlobalEventDispatcher.getInstance().dispatchEvent(new ProjectEvent(ProjectEvent.FLEX_SDK_UDPATED));
		}

		private function onSDKPopupEdit(event:Event):void
		{
			var sdk:SDKReferenceVO = sdkPopup.selectedSDK;
			this.editSDK(sdk);
		}

		private function onSDKPopupClosed(event:Event):void
		{
			var selectedSDK:SDKReferenceVO = sdkPopup.selectedSDK;
			if (selectedSDK)
			{
				// in case user deleted the entry
				setting.stringValue = selectedSDK.path;
				lblValue.text = selectedSDK.name;
				if (selectedSDK.status == SDKUtils.BUNDLED) lblValue.text += " (Bundled)";
				hideDefineSDKLink(selectedSDK.name);
			}

			sdkPopupWrapper.stage.removeEventListener(Event.RESIZE, sdkPopup_stage_resizeHandler);
			sdkPopup.removeEventListener(SDKSelectorView.EVENT_SDK_ADD, onSDKPopupAdd);
			sdkPopup.removeEventListener(SDKSelectorView.EVENT_SDK_REMOVE, onSDKPopupRemove);
			sdkPopup.removeEventListener(SDKSelectorView.EVENT_SDK_EDIT, onSDKPopupEdit);
			PopUpManager.removePopUp(sdkPopupWrapper);
			sdkPopup.removeEventListener(Event.CLOSE, onSDKPopupClosed);
			sdkPopup = null;

			if (selectedSDK)
			{
				setting.dispatchEvent(new Event(AbstractSetting.PATH_SELECTED));
			}
		}

		private function openFile(fileDir:Object):void
		{
			setting.stringValue = fileDir.nativePath;
			if (isDropDown) dropdownListItems.addItem(fileDir.nativePath);

			cmbPath.selectedItem = setting.stringValue;
			setting.dispatchEvent(new Event(AbstractSetting.PATH_SELECTED));

		}

		protected function onPathRendererCreationComplete(event:FlexEvent):void
		{
			dropdownListItems = setting.dropdownListItems;

			// process this following only in case of project's custom SDK
			if (isSDKPath && setting.stringValue)
			{
				// check if sdk is available to the user
				var isFound:SDKReferenceVO = UtilsCore.getUserDefinedSDK(setting.stringValue, "path");
				if (!isFound)
				{
					hgUndefinedSDK.visible = hgUndefinedSDK.includeInLayout = true;
				}
				else
				{
					hideDefineSDKLink(isFound.name);
					lblValue.text = isFound.name;
					if (isFound.status == SDKUtils.BUNDLED) lblValue.text += " (Bundled)";
				}
			}

			if (isDropDown)
			{
				cmbPath.setStyle("contentBackgroundColor", "#FFFFFF");
				cmbPath.selectedItem = setting.provider[setting.name];
			}

			setting.dispatchEvent(new Event(AbstractSetting.PATH_SELECTED));

			// validate path
			if (!isSDKPath && setting.stringValue && !isDropDown)
			{
				if (!(new FileLocation(setting.stringValue).fileBridge.exists))
					setMessage("Path is not accessible.", AbstractSetting.MESSAGE_CRITICAL);
			}
			else
			{
				setMessage(this.message, this.messageType);
			}
		}

		private function hideDefineSDKLink(withSDKName:String):void
		{
			if (hgUndefinedSDK.visible)
			{
				hgUndefinedSDK.visible = hgUndefinedSDK.includeInLayout = false;

				// updates to project custom SDK values
				if (setting.provider.hasOwnProperty("buildOptions"))
				{
					setting.provider.buildOptions.customSDKPath = withSDKName;
					setting.provider.swfOutput.swfVersion = SDKUtils.getSdkSwfMajorVersion(withSDKName);
				}
				// application default sdk
				else
				{
					setting.provider.hasOwnProperty(setting.name)
					{
						setting.provider[setting.name] = withSDKName;
					}
				}
			}
		}

		private function editSDK(sdk:SDKReferenceVO):void
		{
			if (!sdkPathPopup)
			{
				sdkPathPopup = new SDKDefineView();
				sdkPathPopup.sdk = sdk;
				sdkPathPopup.showSandboxWarning = ConstantsCoreVO.IS_MACOS && ConstantsCoreVO.IS_APP_STORE_VERSION;
				sdkPathPopupWrapper = new FeathersUIWrapper(sdkPathPopup);
				PopUpManager.addPopUp(sdkPathPopupWrapper, DisplayObject(parentApplication), true);
				sdkPathPopup.addEventListener(Event.CLOSE, onSDKPathPopupClosed);
				PopUpManager.centerPopUp(sdkPathPopupWrapper);
				sdkPathPopupWrapper.assignFocus("top");
				sdkPathPopupWrapper.stage.addEventListener(Event.RESIZE, sdkPathPopup_stage_resizeHandler, false, 0, true);
			}
			else
			{
				PopUpManager.bringToFront(sdkPathPopupWrapper);
			}
		}

		protected function onSDKAddition(event:MouseEvent):void
		{
			this.editSDK(null);
		}

		protected function sdkPathPopup_stage_resizeHandler(event:Event):void
		{
			PopUpManager.centerPopUp(sdkPathPopupWrapper);
		}

		protected function onSDKPathPopupClosed(event:Event):void
		{
			if (!sdkPathPopup)
			{
				return;
			}

			var newSDK:SDKReferenceVO = sdkPathPopup.sdk;
			// detects been new or edit situation
			if (newSDK is SDKReferenceVO)
			{
				// check if the sdk is new or one that edited
				if (model.userSavedSDKs.getItemIndex(newSDK) == -1)
				{
					// add sdk
					var sdkResult:SDKReferenceVO = SDKUtils.isSDKAlreadySaved(newSDK);
					if (sdkResult != newSDK)
					{
						// don't add if said sdk already added
						Alert.show("SDK is already added in the list.", ConstantsCoreVO.MOONSHINE_IDE_LABEL);
					}
				}
				else
				{
					// edited sdk
					model.userSavedSDKs.refresh();
				}
				GlobalEventDispatcher.getInstance().dispatchEvent(new ProjectEvent(ProjectEvent.FLEX_SDK_UDPATED));
				if (sdkPopup)
				{
					sdkPopup.sdks = new feathers.data.ArrayCollection(model.userSavedSDKs.source);
				}
			}

			sdkPathPopupWrapper.stage.removeEventListener(Event.RESIZE, sdkPathPopup_stage_resizeHandler);
			PopUpManager.removePopUp(sdkPathPopupWrapper);
			sdkPathPopup.removeEventListener(Event.CLOSE, onSDKPathPopupClosed);
			sdkPathPopup = null;
		}

		protected function onDropDownListPathChange(event:IndexChangeEvent):void
		{
			setting.stringValue = cmbPath.selectedItem;
			setting.dispatchEvent(new Event(AbstractSetting.PATH_SELECTED));
		}

		protected function onPathRendererRemovedFromStage(event:Event):void
		{
			// in case of MXMLC Compiler Plugin
			onSDKPathPopupClosed(null);
		}
		]]>
	</fx:Script>

	<s:HGroup width="100%">
		<s:Label styleName="uiTextSettingsLabel"
				 text="{setting.label}" />

		<mx:Spacer width="100%"/>

		<s:Button label="Clear"
				  skinClass="{LinkButtonSkin}"
				  click="clearPath()" />

		<s:Button label="Change"
				  skinClass="{LinkButtonSkin}"
				  click="selectPath()" />

		<s:Button label="Go to Folder"
				  skinClass="{LinkButtonSkin}"
				  click="openPopupToAccessArbitraryFolder()"
					includeInLayout="{ConstantsCoreVO.IS_MACOS &amp;&amp; !isSDKPath}" visible="{ConstantsCoreVO.IS_MACOS &amp;&amp; !isSDKPath}"/>
	</s:HGroup>

	<s:Label id="lblValue"
			 styleName="uiTextSettingsValue"
			 paddingTop="10" width="98%"
			 text="{setting.stringValue? setting.stringValue : 'Not Set' }" visible="{!isDropDown}" includeInLayout="{!isDropDown}"  />
	<s:DropDownList id="cmbPath"
					width="100%" height="24"
					dataProvider="{dropdownListItems}"
					visible="{isDropDown}" includeInLayout="{isDropDown}"
					requireSelection="true"
					change="onDropDownListPathChange(event)"/>
	<s:Label id="lblMessage"
			 styleName="uiTextSettingsLabel" color="#666666" width="100%" maxDisplayedLines="3"
			 fontSize="12" includeInLayout="false" visible="false"/>
	<s:HGroup id="hgUndefinedSDK"
			  width="100%" verticalAlign="middle" gap="4"
			  visible="false" includeInLayout="false">
		<s:Label text="This SDK is not defined."
				 styleName="uiTextSettingsLabel" color="#ff0000" fontSize="12"/>
		<s:Button label="Define it now"
				  skinClass="{LinkButtonSkin}" useHandCursor="true" buttonMode="true"
				  click="onSDKAddition(event)"/>
	</s:HGroup>
</s:VGroup>