<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
//
//  Copyright (C) STARTcloud, Inc. 2015-2022. All rights reserved.
//
//  This program is free software: you can redistribute it and/or modify
//  it under the terms of the Server Side Public License, version 1,
//  as published by MongoDB, Inc.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  Server Side Public License for more details.
//
//  You should have received a copy of the Server Side Public License
//  along with this program. If not, see
//
//  http://www.mongodb.com/licensing/server-side-public-license
//
//  As a special exception, the copyright holders give permission to link the
//  code of portions of this program with the OpenSSL library under certain
//  conditions as described in each individual source file and distribute
//  linked combinations including the program with the OpenSSL library. You
//  must comply with the Server Side Public License in all respects for
//  all of the code used other than as permitted herein. If you modify file(s)
//  with this exception, you may extend this exception to your version of the
//  file(s), but you are not obligated to do so. If you do not wish to do so,
//  delete this exception statement from your version. If you delete this
//  exception statement from all source files in the program, then also delete
//  it in the license file.
//
////////////////////////////////////////////////////////////////////////////////
-->
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   implements="actionScripts.ui.IContentWindow"
			   width="100%" height="100%"
			   initialize="onInitialized(event)" creationComplete="onCreationCompletes()"
			   xmlns:components="components.*">
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			
			import actionScripts.events.GeneralEvent;
			import actionScripts.events.GlobalEventDispatcher;
			import actionScripts.events.StartupHelperEvent;
			import actionScripts.interfaces.IHelperMoonshineBridge;
			import actionScripts.locator.IDEModel;
			import actionScripts.plugin.settings.vo.PluginSetting;
			import actionScripts.plugins.domino.DominoPlugin;
			import actionScripts.plugins.git.GitHubPlugin;
			import actionScripts.ui.IContentWindow;
			import actionScripts.ui.tabview.CloseTabEvent;
			import actionScripts.utils.EnvironmentUtils;
			import actionScripts.utils.HelperUtils;
			import actionScripts.utils.MSDKIdownloadUtil;
			import actionScripts.utils.PathSetupHelperUtil;
			import moonshine.haxeScripts.valueObjects.ComponentTypes;
			import moonshine.haxeScripts.valueObjects.ComponentVO;
			import actionScripts.valueObjects.ConstantsCoreVO;
			import actionScripts.valueObjects.FileWrapper;
			import actionScripts.valueObjects.HelperConstants;
			
			import air.update.events.DownloadErrorEvent;
			
			import moonshine.events.HelperEvent;
			
			private static const LABEL:String = "Getting Started";
			
			public var dependencyCheckUtil:IHelperMoonshineBridge;
			public var environmentUtil:EnvironmentUtils;
			
			[Bindable] private var sdkInstallerInstallingMess:String;
			
			private var model:IDEModel = IDEModel.getInstance();
			private var dispatcher:GlobalEventDispatcher = GlobalEventDispatcher.getInstance();
			private var msdkiDownloadUtil:MSDKIdownloadUtil = MSDKIdownloadUtil.getInstance();
			private var workspaceWrapper:FileWrapper;
			private var progressTimer:Timer;
			
			//--------------------------------------------------------------------------
			//
			//  INTERFACE API
			//
			//--------------------------------------------------------------------------
			
			public function get longLabel():String
			{
				return LABEL;
			}
			
			public function get label():String
			{
				return LABEL;
			}
			
			public function isChanged():Boolean
			{
				return false;
			}
			
			public function isEmpty():Boolean
			{
				return false;
			}
			
			public function save():void
			{
			}
			
			//--------------------------------------------------------------------------
			//
			//  PUBLIC API
			//
			//--------------------------------------------------------------------------
			
			public function onInvokeEvent(componentId:String, path:String=null):void
			{
				if (viewHelper.isConfigurationLoaded)
					viewHelper.updateComponentStatusAsDownloaded(componentId, path);
				else
					PathSetupHelperUtil.updateFieldPath(componentId, path);
			}
			
			public function dispose():void
			{
				stopMessageAnimateProcess();
				addRemoveInstallerDownloadEvents(false);
			}
			
			//--------------------------------------------------------------------------
			//
			//  PRIVATE API
			//
			//--------------------------------------------------------------------------
			
			private function onInitialized(event:FlexEvent):void
			{
				viewHelper.dependencyCheckUtil = dependencyCheckUtil;
				viewHelper.environmentUtil = environmentUtil;
				
				msdkiDownloadUtil.addEventListener(MSDKIdownloadUtil.EVENT_NEW_VERSION_DETECTED, onNewVersionDetected, false, 0, true);
			}
			
			protected function onCreationCompletes():void
			{
				updateUI();
				dispatcher.addEventListener(StartupHelperEvent.REFRESH_GETTING_STARTED, onRefreshRequest, false, 0, true);
				this.addEventListener(CloseTabEvent.EVENT_TAB_CLOSED, onTabClosed);
			}
			
			private function updateUI():void
			{
				cbDNS.selected = ConstantsCoreVO.IS_GETTING_STARTED_DNS;
				
				var ps:PluginSetting = new PluginSetting(ConstantsCoreVO.MOONSHINE_IDE_LABEL +" is Installed. What's Next?", ConstantsCoreVO.MOONSHINE_IDE_LABEL +" Project Team", "Moonshine includes an extensive set of features by default. Some optional features (shown below) require access to third-party software. If you already have the third-party software installed, press the Configure button, otherwise press Download button.", false);
				body.addElementAt(ps.renderer, 0);
			}
			
			//--------------------------------------------------------------------------
			//
			//  PRIVATE LISTENERS
			//
			//--------------------------------------------------------------------------
			
			private function onNewVersionDetected(event:Event):void
			{
				sdkInstallerInstallingMess = "Found new version - Moonshine SDK Installer is downloading. Please wait.";
			}
			
			private function onTabClosed(event:CloseTabEvent):void
			{
				msdkiDownloadUtil.removeEventListener(MSDKIdownloadUtil.EVENT_NEW_VERSION_DETECTED, onNewVersionDetected);
				dispatcher.removeEventListener(StartupHelperEvent.REFRESH_GETTING_STARTED, onRefreshRequest);
				this.removeEventListener(CloseTabEvent.EVENT_TAB_CLOSED, onTabClosed);
			}
			
			private function runAppStoreHelper(event:HelperEvent):void
			{
				if (ConstantsCoreVO.IS_WINDOWS)
				{
					if (!msdkiDownloadUtil.is64BitSDKInstallerExists())
					{
						addRemoveInstallerDownloadEvents(true);
						startMessageAnimateProcess();
					}
					else
					{
						sdkInstallerInstallingMess = "Moonshine SDK Installer requested. This may take a few seconds.";
					}
				}
				
				msdkiDownloadUtil.runOrDownloadSDKInstaller();
			}
			
			private function onOpenSettings(event:HelperEvent):void
			{
				var component:ComponentVO = event.data as ComponentVO;
				if ((component.type == ComponentTypes.TYPE_GIT || component.type == ComponentTypes.TYPE_SVN) && 
					ConstantsCoreVO.IS_MACOS)
				{
					var gitComponent:ComponentVO = HelperUtils.getComponentByType(ComponentTypes.TYPE_GIT);
					var svnComponent:ComponentVO = HelperUtils.getComponentByType(ComponentTypes.TYPE_SVN);
					
					dispatcher.dispatchEvent(new Event(GitHubPlugin.RELAY_SVN_XCODE_REQUEST));
					gitComponent.hasWarning = svnComponent.hasWarning = null;
				}
				else if (component.type == ComponentTypes.TYPE_NOTES && ConstantsCoreVO.IS_MACOS)
				{
					dispatcher.dispatchEvent(new Event(DominoPlugin.RELAY_MAC_NOTES_PERMISSION_REQUEST));
				}
				else
				{
					PathSetupHelperUtil.openSettingsViewFor(component.type);
				}
			}
			
			private function onAnyComponentDownloaded(event:HelperEvent):void
			{
				// autoset moonshine internal fields as appropriate
				var component:ComponentVO = event.data as ComponentVO;
				PathSetupHelperUtil.updateFieldPath(component.type, component.installToPath);
			}
			
			private function onUnzipCompleted(event:GeneralEvent):void
			{
				stopMessageAnimateProcess();
				addRemoveInstallerDownloadEvents(false);
			}
			
			private function onSDKInstallerDownloadError(event:DownloadErrorEvent):void
			{
				addRemoveInstallerDownloadEvents(false);
				Alert.show(event.text, "Error!");
			}
			
			private function addRemoveInstallerDownloadEvents(add:Boolean):void
			{
				if (add)
				{
					msdkiDownloadUtil.addEventListener(GeneralEvent.DONE, onUnzipCompleted, false, 0, true);
					msdkiDownloadUtil.addEventListener(DownloadErrorEvent.DOWNLOAD_ERROR, onSDKInstallerDownloadError, false, 0, true);
				}
				else
				{
					msdkiDownloadUtil.removeEventListener(GeneralEvent.DONE, onUnzipCompleted);
					msdkiDownloadUtil.removeEventListener(DownloadErrorEvent.DOWNLOAD_ERROR, onSDKInstallerDownloadError);
				}
			}
			
			private function stopMessageAnimateProcess():void
			{
				if (progressTimer) 
				{
					if (progressTimer.running) progressTimer.stop();
					progressTimer.removeEventListener(TimerEvent.TIMER, onTimerTick);
				}
				sdkInstallerInstallingMess = null;
				progressTimer = null;
			}
			
			private function startMessageAnimateProcess():void
			{
				stopMessageAnimateProcess();
				
				sdkInstallerInstallingMess = "Moonshine SDK Installer is downloading. Please wait.";
				progressTimer = new Timer(1000);
				progressTimer.addEventListener(TimerEvent.TIMER, onTimerTick);
				progressTimer.start();
			}
			
			private function onTimerTick(event:TimerEvent):void
			{
				if (sdkInstallerInstallingMess.lastIndexOf("..") == -1) sdkInstallerInstallingMess += ".";
				else sdkInstallerInstallingMess = sdkInstallerInstallingMess.replace("..", ".");
			}
			
			protected function onRefreshRequest(event:Event):void
			{
				viewHelper.checkForUpdate();
			}
			
			protected function onDNSchange(event:Event):void
			{
				ConstantsCoreVO.IS_GETTING_STARTED_DNS = cbDNS.selected;
				dispatcher.dispatchEvent(new StartupHelperEvent(StartupHelperEvent.EVENT_DNS_GETTING_STARTED));
			}
			
		]]>
	</fx:Script>
	
	<s:Rect width="100%" height="100%">
		<s:fill>
			<s:SolidColor color="0xf5f5f5"/>
		</s:fill>
	</s:Rect>
	
	<s:VGroup id="body"
			  width="100%" height="100%"
			  paddingLeft="12" paddingTop="24" paddingBottom="12" paddingRight="12"
			  horizontalAlign="center">
		<s:Button label="Download Third-party Software"
				  styleName="greyButton"
				  click="runAppStoreHelper(null)"/>
		<s:Label text="{sdkInstallerInstallingMess}"
				 includeInLayout="{sdkInstallerInstallingMess != null}" visible="{sdkInstallerInstallingMess != null}"
				 styleName="smallText" paddingBottom="10"/>
		<components:HelperView id="viewHelper" 
							   width="100%" height="100%"
							   isRunningInsideMoonshine="true"
							   DOWNLOAD_COMPONENT="runAppStoreHelper(event)"
							   OPEN_MOON_SETTINGS="onOpenSettings(event)"
							   COMPONENT_DOWNLOADED="onAnyComponentDownloaded(event)"/>
		<s:CheckBox id="cbDNS"
					label="Do not show this tab on startup"
					change="onDNSchange(event)"/>
	</s:VGroup>
	
	<s:HGroup verticalAlign="middle" horizontalAlign="right"
			  top="10" right="10">
		<s:Label text="Updating.."
				 styleName="uiText"
				 fontSize="12"
				 visible="{HelperConstants.IS_DETECTION_IN_PROCESS}"/>
		<s:Image source="@Embed('/assets/images/icoRefreshGettingStarted.png')"
				 buttonMode="true" toolTip="Refresh List"
				 enabled="{!HelperConstants.IS_DETECTION_IN_PROCESS}" alpha="{HelperConstants.IS_DETECTION_IN_PROCESS ? .6 : 1}"
				 click="onRefreshRequest(event)"/>
	</s:HGroup>
</s:Group>