<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
//
//  Copyright (C) STARTcloud, Inc. 2015-2022. All rights reserved.
//
//  This program is free software: you can redistribute it and/or modify
//  it under the terms of the Server Side Public License, version 1,
//  as published by MongoDB, Inc.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  Server Side Public License for more details.
//
//  You should have received a copy of the Server Side Public License
//  along with this program. If not, see
//
//  http://www.mongodb.com/licensing/server-side-public-license
//
//  As a special exception, the copyright holders give permission to link the
//  code of portions of this program with the OpenSSL library under certain
//  conditions as described in each individual source file and distribute
//  linked combinations including the program with the OpenSSL library. You
//  must comply with the Server Side Public License in all respects for
//  all of the code used other than as permitted herein. If you modify file(s)
//  with this exception, you may extend this exception to your version of the
//  file(s), but you are not obligated to do so. If you do not wish to do so,
//  delete this exception statement from your version. If you delete this
//  exception statement from all source files in the program, then also delete
//  it in the license file.
//
////////////////////////////////////////////////////////////////////////////////
-->
<resizableTitleWindow:ResizableTitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
										   xmlns:s="library://ns.adobe.com/flex/spark"
										   xmlns:mx="library://ns.adobe.com/flex/mx"
										   xmlns:resizableTitleWindow="actionScripts.ui.resizableTitleWindow.*"
										   xmlns:view="actionScripts.plugin.findreplace.view.*"
										   xmlns:controls="components.controls.*"
										   backgroundColor="0xf5f5f5" title="Deploy Domino Database"
										   width="600" minWidth="400" autoLayout="true"
										   skinClass="components.skins.ResizableTitleWindowSkin"
										   creationComplete="onCreationCompletes(event)">
	<fx:Script>
		<![CDATA[
		import actionScripts.events.SettingsEvent;
		import actionScripts.interfaces.IDeployDominoDatabaseProject;
		import actionScripts.locator.IDEModel;
		import actionScripts.plugin.ondiskproj.vo.OnDiskProjectVO;
		import actionScripts.plugins.vagrant.VagrantPlugin;
		import actionScripts.plugins.vagrant.utils.VagrantUtil;
		import actionScripts.plugins.vagrant.vo.VagrantInstanceState;
		import actionScripts.plugins.vagrant.vo.VagrantInstanceVO;
		import actionScripts.utils.FileUtils;
		import actionScripts.utils.uploadUsingURLLoader.FileUploaderUsingURLLoader;
		import actionScripts.valueObjects.FileWrapper;

		import mx.collections.ArrayCollection;

		import mx.events.FlexEvent;

		import actionScripts.events.GlobalEventDispatcher;

		import mx.validators.Validator;

		import spark.components.Alert;

		public static const EVENT_START_CONVERSION:String = "eventStartConversion";

		public var instances:ArrayCollection;

		[Bindable]
		private var project:IDeployDominoDatabaseProject;
		[Bindable]
		private var nsfConverterInstances:ArrayCollection;
		private var dispatcher:GlobalEventDispatcher = GlobalEventDispatcher.getInstance();
		private var instanceStateCheckCount:int;

		private var _uploadRequestReturn:Object;
		public function get uploadRequestReturn():Object
		{
			return _uploadRequestReturn;
		}

		public function get selectedInstance():VagrantInstanceVO
		{
			return ddlInstances.selectedItem;
		}

		public function get jsonFilePath():String
		{
			return txtFile.text;
		}

		public function get localDatabasePath():String
		{
			return project.localDatabase;
		}

		public function reset():void
		{
			enableUI();
		}

		public function close():void
		{
			closeThis();
		}

		override protected function closeThis():void
		{
			dispatcher.removeEventListener(VagrantUtil.EVENT_INSTANCE_STATE_CHECK_COMPLETES, onIndividualInstanceChecked);

			super.closeThis();
		}

		protected function onCreationCompletes(event:FlexEvent):void
		{
			if (IDEModel.getInstance().activeProject &&
					(IDEModel.getInstance().activeProject is IDeployDominoDatabaseProject))
			{
				project = (IDEModel.getInstance().activeProject as IDeployDominoDatabaseProject);
			}

			var selectedTreeItem:FileWrapper = IDEModel.getInstance().mainView.getTreeViewPanel().selectedItem ?
					IDEModel.getInstance().mainView.getTreeViewPanel().selectedItem as FileWrapper : null;
			if (selectedTreeItem && selectedTreeItem.file.fileBridge.extension &&
					selectedTreeItem.file.fileBridge.extension.toLowerCase() == "json")
			{
				txtFile.text = selectedTreeItem.file.fileBridge.nativePath;
			}

			disableUI();
			dispatcher.addEventListener(VagrantUtil.EVENT_INSTANCE_STATE_CHECK_COMPLETES, onIndividualInstanceChecked, false, 0, true);
			VagrantUtil.checkStates(instances);
		}

		private function onIndividualInstanceChecked(event:Event):void
		{
			instanceStateCheckCount++;
			if (instances && (instances.length == instanceStateCheckCount))
			{
				enableUI();
				nsfConverterInstances = new ArrayCollection();
				for each (var instance:VagrantInstanceVO in instances)
				{
					if (instance.capabilities && (instance.capabilities.indexOf("import-domino-json") != -1) &&
							(instance.state == VagrantInstanceState.READY))
					{
						nsfConverterInstances.addItem(instance);
					}
				}
			}
		}

		private function onConfigure():void
		{
			dispatcher.dispatchEvent(new SettingsEvent(SettingsEvent.EVENT_OPEN_SETTINGS, VagrantPlugin.NAMESPACE));
		}

		private function onNSFBrowse():void
		{
			IDEModel.getInstance().fileCore.browseForOpen(
					"Select File to Import",
					onJSONFileSelected,
					null,
					["*.json"],
					txtFile.text ? txtFile.text : null
			);
		}

		private function onJSONFileSelected(file:Object):void
		{
			txtFile.text = file.nativePath;
		}

		private function onSubmit():void
		{
			// validations
			var tmpArr:Array = new Array(svPath);
			if (Validator.validateAll(tmpArr).length != 0)
			{
				return;
			}

			Alert.OK_LABEL = "OK";
			if (!FileUtils.isPathExists(txtFile.text))
			{
				Alert.show("Local database path doesn't exist.", "Error!");
				return;
			}
			if (!ddlInstances.selectedItem)
			{
				Alert.show("A valid Vagrant Instance is missing.", "Error!");
				return;
			}

			disableUI();
			dispatchEvent(new Event(EVENT_START_CONVERSION));
		}

		private function disableUI():void
		{
			btnBrowse.enabled = false;
			btnSubmit.enabled = false;
			ddlInstances.enabled = false;
		}

		private function enableUI():void
		{
			btnBrowse.enabled = true;
			btnSubmit.enabled = true;
			ddlInstances.enabled = true;
		}
		]]>
	</fx:Script>
	<fx:Declarations>
		<mx:StringValidator id="svPath" source="{txtFile}" property="text" trigger="{btnSubmit}"/>
	</fx:Declarations>

	<resizableTitleWindow:layout>
		<s:VerticalLayout paddingLeft="12" paddingBottom="12" paddingTop="9" paddingRight="12"/>
	</resizableTitleWindow:layout>

	<s:Label text="File to Import (*.json):"/>
	<s:HGroup width="100%" verticalAlign="middle">
		<view:PromptTextInput id="txtFile"
							  width="100%" styleName="textInput" prompt="Select path.." editable="false"/>
		<s:Button id="btnBrowse"
				  label="Browse" click="onNSFBrowse()" styleName="darkButton"/>
	</s:HGroup>

	<s:Label text="Vagrant Instance:"/>
	<s:HGroup width="100%" verticalAlign="middle">
		<s:DropDownList id="ddlInstances"
						width="100%" height="26"
						dataProvider="{nsfConverterInstances}" labelField="title" requireSelection="true"/>
		<s:Button label="Configure" styleName="darkButton"
				  click="onConfigure()"/>
	</s:HGroup>

	<resizableTitleWindow:controlBarContent>
		<s:Spacer width="100%"/>
		<s:Button id="btnSubmit"
				  label="Submit" styleName="darkButton"
					click="onSubmit()"/>
	</resizableTitleWindow:controlBarContent>
</resizableTitleWindow:ResizableTitleWindow>