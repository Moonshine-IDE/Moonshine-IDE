<?xml version="1.0" encoding="utf-8"?>
<resizableTitleWindow:ResizableTitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 width="500" minWidth="400" minHeight="100" autoLayout="true"
		 skinClass="components.skins.ResizableTitleWindowSkin" xmlns:resizableTitleWindow="actionScripts.ui.resizableTitleWindow.*" xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:view="actionScripts.plugin.findreplace.view.*"
		 creationComplete="onCreationCompletes()">
	<fx:Script>
		<![CDATA[
		import actionScripts.events.GlobalEventDispatcher;
		import actionScripts.plugin.console.ConsoleOutputEvent;
		import actionScripts.plugins.versionControl.event.VersionControlEvent;
		import actionScripts.valueObjects.ConstantsCoreVO;

		import mx.events.CloseEvent;
			import mx.validators.Validator;
			
			import actionScripts.events.GeneralEvent;
			
			public static const VALIDATE_NAME:String = "validateName";
			
			[Bindable] public var isGitAvailable:Boolean;
			public var submitObject:Object;

			private var dispatcher:GlobalEventDispatcher = GlobalEventDispatcher.getInstance();

			override protected function closeByCrossSign(event:Event):void
			{
				dispatcher.removeEventListener(VersionControlEvent.REPOSITORY_AUTH_CANCELLED, onAuthRequestCancelled);
				super.closeByCrossSign(event);
			}
			
			public function onNameValidatedByGit(localValue:String, remoteValue:String, isMultipleOrigin:Boolean, originWhereBranchFound:String=null):void
			{
				// in case of branch exists value is null
				if (!localValue && !remoteValue)
				{
					submitObject = {name:txtName.text, pushToRemote:cbPush.selected};
					dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
					return;
				}

				btnCommit.enabled = true;

				var isLocalExists:Boolean;
				var isRemoteExists:Boolean;
				if (localValue)
				{
					localValue = localValue.replace("\n", "");
					if (localValue.match(/fatal: .*/) || localValue.match(/Error: .*/) || localValue.toLowerCase().match(/was unexpected at this time/))
					{
						showError(localValue);
						//dispatcher.dispatchEvent(new ConsoleOutputEvent(ConsoleOutputEvent.CONSOLE_OUTPUT, localValue, false, false, ConsoleOutputEvent.TYPE_ERROR));
					}
					else
					{
						isLocalExists = true;
					}
				}

				if (remoteValue)
				{
					remoteValue = remoteValue.replace("\n", "");
					if (remoteValue.match(/fatal: .*/) || localValue.match(/Error: .*/) || remoteValue.toLowerCase().match(/was unexpected at this time/))
					{
						showError(remoteValue);
						//dispatcher.dispatchEvent(new ConsoleOutputEvent(ConsoleOutputEvent.CONSOLE_OUTPUT, remoteValue, false, false, ConsoleOutputEvent.TYPE_ERROR));
					}
					else
					{
						isRemoteExists = true;
					}
				}

				if (isLocalExists || isRemoteExists)
				{
					var errorMessage:String = "";
					if (isLocalExists)
					{
						errorMessage = "Local branch already exists.";
					}
					else if (isRemoteExists)
					{
						errorMessage = "Branch already exists in remote"+ (isMultipleOrigin ? ": "+ originWhereBranchFound : "") +".";
					}

					showError(errorMessage);
				}
				
				/*
				 * @local
				 */
				function showError(value:String):void
				{
					lblMess.text = value;
					lblMess.setStyle("color", 0xe32636);
					hgError.includeInLayout = hgError.visible = true;
				}
			}

			private function onCreationCompletes():void
			{
				txtName.setFocus();
				dispatcher.addEventListener(VersionControlEvent.REPOSITORY_AUTH_CANCELLED, onAuthRequestCancelled, false, 0, true);
			}
			
			private function onSubmit():void
			{
				// was unexpected at this time
				// fatal:
				
				var tmpArr : Array = new Array(sValidator);
				if (Validator.validateAll(tmpArr).length == 0)
				{
					lblMess.text = "Validating..";
					lblMess.setStyle("color", 0xF4F4F4);
					hgError.includeInLayout = hgError.visible = true;

					btnCommit.enabled = false;
					dispatchEvent(new GeneralEvent(VALIDATE_NAME, txtName.text));
				}
			}

			private function onAuthRequestCancelled(event:VersionControlEvent):void
			{
				btnCommit.enabled = true;
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<mx:StringValidator id="sValidator" source="{txtName}" property="text" trigger="{btnCommit}" triggerEvent="click"/>
	</fx:Declarations>
	
	<s:VGroup height="100%" width="100%" 
			  paddingLeft="12" paddingBottom="3" paddingTop="9" paddingRight="13">
		<view:PromptTextInput id="txtName"
							  width="100%" styleName="textInput"
							  prompt="New Branch Name" enter="onSubmit()"
							  restrict="a-zA-Z0-9`!@#$%()_+={}';.\-\/\&amp;"/>
		<s:CheckBox id="cbPush"
					label="Push newly created branch to remote"/>
		<s:HGroup width="100%" verticalAlign="middle" horizontalAlign="center"
				  visible="{!isGitAvailable}" includeInLayout="{!isGitAvailable}">
			<s:Image source="@Embed('/elements/images/iconExclamationRed.png')"/>
			<s:Label text="You need Git installed on your system to continue."/>
		</s:HGroup>
		<s:HGroup id="hgError"
				  width="100%" verticalAlign="middle" horizontalAlign="center"
				  visible="false" includeInLayout="false">
			<s:Image source="@Embed('/elements/images/iconExclamationRed.png')"/>
			<s:Label id="lblMess"
					 maxDisplayedLines="3"/>
		</s:HGroup>
	</s:VGroup>
	
	<resizableTitleWindow:controlBarContent>
		<s:Button id="btnCommit" label="Create Branch"
				  styleName="darkButton" click="onSubmit()"/>
	</resizableTitleWindow:controlBarContent>
</resizableTitleWindow:ResizableTitleWindow>